#id: DOq063
#description: >-
#  All diseases in the 'genetic disease' branch with their inheritance pattern,
#  when asserted in a direct logical axiom
#input: doid-merged.owl
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX DOID: <http://purl.obolibrary.org/obo/DOID_>
PREFIX GENO: <http://purl.obolibrary.org/obo/GENO_>
PREFIX IAO: <http://purl.obolibrary.org/obo/IAO_>

SELECT ?iri ?id ?label ?definition ?geno_id ?geno_label
FROM <http://purl.obolibrary.org/obo/doid/doid-merged.owl>
WHERE {
  ?iri a owl:Class ;
    oboInOwl:id ?id ;
    rdfs:label ?label ;
    rdfs:subClassOf* DOID:630 .  # 'genetic disease'

  OPTIONAL { ?iri IAO:0000115 ?definition }

  # inheritance pattern linked to diseases via OWL logical axioms
  OPTIONAL {
    ?iri owl:equivalentClass|rdfs:subClassOf ?axiom .

    ?axiom (owl:intersectionOf|owl:unionOf|owl:Restriction|rdf:first|rdf:rest|owl:someValuesFrom|owl:onClass)* ?geno_iri .

    ?geno_iri rdfs:subClassOf* GENO:0000141 ;  # 'inheritance pattern'
      rdfs:label ?geno_label .
    BIND(
      REPLACE(str(?geno_iri), ".*/([^_]+)_(\\d+)", "$1:$2") AS ?geno_id
    )
  }
}