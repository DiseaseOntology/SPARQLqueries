#id: DOq...
#description: List all exact acronyms (active diseases only)
#input: doid.owl
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT ?id ?label ?acronym
WHERE {
  ?class a owl:Class ;
    oboInOwl:hasOBONamespace "disease_ontology" ;
    oboInOwl:id ?id ;
    rdfs:label ?label ;
    oboInOwl:hasExactSynonym ?acronym .

  [] a owl:Axiom ;
    owl:annotatedSource ?class ;
    owl:annotatedProperty oboInOwl:hasExactSynonym ;
    owl:annotatedTarget ?acronym ;
    oboInOwl:hasSynonymType obo:OMO_0003012 .

  FILTER NOT EXISTS { ?class owl:deprecated true }
}
ORDER BY ?id ?acronym