#id: DOq059
#description: All diseases with a genetic susceptibility (from OMIM)
#input: doid-merged.owl
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT ?iri ?label ?omim_susc_iri ?omim_susc_label
FROM <http://purl.obolibrary.org/obo/doid/doid-merged.owl>
WHERE {
  ?iri a owl:Class ;
    rdfs:label ?label ;
    owl:equivalentClass|rdfs:subClassOf ?anon_class .
  FILTER(CONTAINS(str(?iri), "DOID"))

  # OWL subclass of logical axiom(s) indicating contribution to disease
  ?anon_class (owl:intersectionOf|owl:unionOf|owl:Restriction|rdf:first|rdf:rest|owl:someValuesFrom|owl:onClass)* ?omim_susc_iri .

  ?omim_susc_iri rdfs:label ?omim_susc_label ;
    rdfs:subClassOf* obo:MIM_000000 .
}
