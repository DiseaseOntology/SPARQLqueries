# DQ8. Diseases with PubMed sources (limited to first 10 results)
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT ?id ?label ?pubmed_src
WHERE {
    ?class a owl:Class ;
        oboInOwl:id ?id ;
        rdfs:label ?label ;
        obo:IAO_0000115 ?def .

    FILTER NOT EXISTS { ?class owl:deprecated ?obsolete .  }

    ?anon_class owl:annotatedSource ?class ;
        owl:annotatedProperty obo:IAO_0000115 ;
        owl:annotatedTarget ?def ;
        oboInOwl:hasDbXref ?def_src .

    FILTER( CONTAINS( lcase( str(?def_src) ), "pubmed" ) )
    BIND( STRAFTER( str(?def_src), "url:") AS ?pubmed_src)
}
LIMIT 10