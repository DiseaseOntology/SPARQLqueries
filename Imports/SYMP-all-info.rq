#id: DOq101
#description: All symptoms imported from SYMP and their linked data
#input: doid-merged.owl
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT *
FROM <http://purl.obolibrary.org/obo/doid/doid-merged.owl>
WHERE {
  # general symptoms information
  ?iri a owl:Class ;
    oboInOwl:id ?id ;
    rdfs:label ?label .
  FILTER(CONTAINS(str(?iri), "SYMP"))

  # deprecation status
  OPTIONAL { ?iri owl:deprecated ?deprecated . }

  # alternate ID(s)
  OPTIONAL { ?iri oboInOwl:hasAlternativeId ?alt_id . }

  # term replaced by -- only for deprecated classes
  OPTIONAL { ?iri obo:IAO_0100001 ?replaced_by . }

  # definition
  OPTIONAL {
    ?iri obo:IAO_0000115 ?definition .

    # definition source(s) -- must have at least one when defined
    OPTIONAL {
      [] owl:annotatedSource ?iri ;
        owl:annotatedProperty obo:IAO_0000115 ;
        owl:annotatedTarget ?definition ;
        oboInOwl:hasDbXref ?def_src .
    }
  }

  # comment
  OPTIONAL { ?iri rdfs:comment ?comment . }

  # parent(s) -- must have at least one
  OPTIONAL {
    ?iri rdfs:subClassOf ?parent_IRI .
    FILTER(!isBlank(?parent_IRI))
  }

  # synonym(s)
  OPTIONAL {
    VALUES ?syn_relation {
       oboInOwl:hasExactSynonym oboInOwl:hasBroadSynonym
       oboInOwl:hasNarrowSynonym oboInOwl:hasRelatedSynonym
    }
    ?iri ?syn_relation ?synonym .
  }

  # xref(s)
  OPTIONAL { ?iri oboInOwl:hasDbXref ?xref . }
}