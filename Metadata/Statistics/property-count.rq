#id: DOq009
#description: >-
#  Count each object/annotation property used in DO (excluding imports) by
#  ORIGINAL namespace
#input: doid.owl
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT ?prop_type ?namespace (COUNT(DISTINCT ?prop) AS ?count)
WHERE {
  VALUES ?type { owl:AnnotationProperty owl:ObjectProperty }

  ?prop a ?type .

  BIND(
    IF(
      REGEX(STR(?prop), "[#_]"),
      REPLACE(STR(?prop), "([^_#]+[_#]).*", "$1"),
      REPLACE(STR(?prop), "(.+/).*", "$1")
    ) AS ?namespace
  )

  BIND(STRAFTER(str(?type), "#") AS ?prop_type)
}
GROUP BY ?prop_type ?namespace
ORDER BY DESC(?prop_type) ?namespace